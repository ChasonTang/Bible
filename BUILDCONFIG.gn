if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}

assert(target_os == "android" || target_os == "ios" || target_os == "mac",
       "Unsupported target os.")
assert(target_cpu == "arm" || target_cpu == "arm64" || target_cpu == "x86" ||
           target_cpu == "x64",
       "Unsupported target cpu.")

declare_args() {
  generate_dwarf = true
  is_debug = false

  if (target_os == "android") {
    ndk_path = "~/Library/Android/sdk/ndk/21.4.7075529"
  } else {
    collect_code_coverage = false
  }
  asan = false
  ubsan = false

  enable_qjs_big_number = false
}

declare_args() {
  if (!is_debug) {
    lto = true
  }
}

have_bitcode_flag =
    target_os == "ios" && (target_cpu == "arm" || target_cpu == "arm64") &&
    !is_debug && !lto

declare_args() {
  if (have_bitcode_flag) {
    bitcode = true
  }
}

if (target_os == "android") {
  set_default_toolchain("//toolchain:android")
} else {
  set_default_toolchain("//toolchain:clang")
}
