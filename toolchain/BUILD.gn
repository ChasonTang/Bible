# compiler tools
depfile = "{{output}}.d"
# all tools except "action"
compiler_command = "-c {{source}} -o {{output}} -Wall -Wextra -Werror -Wpedantic -g -MMD -MF $depfile {{include_dirs}} {{defines}} {{cflags}}"

if (debug) {
    test = ""
    compiler_command = string_join(" ", ["-O0", compiler_command])
} else {
# NDEBUG for assert.h
    compiler_command = string_join(" ", ["-Os -flto -DNDEBUG", compiler_command])
}

compiler_command = string_join(" ", [test, compiler_command])

compiler_outputs = [
    "{{source_out_dir}}/{{source_name_part}}.o"
]
linker_outputs = [
    "{{output_dir}}/{{target_output_name}}{{output_extension}}"
]

toolchain("android") {
    compiler_command = string_join(" ", ["-target $android_target --sysroot $ndk_path/toolchains/llvm/prebuilt/darwin-x86_64/sysroot", compiler_command])
    tool("cc") {
        command = string_join(" ", ["$ndk_path/toolchains/llvm/prebuilt/darwin-x86_64/bin/clang -fpic", compiler_command, "{{cflags_cc}}"])
        # -fpic 永远使用 GOT 全局偏移表，更严格
        outputs = compiler_outputs
    }
    tool("stamp") {
        command = "touch {{output}}"
    }
    tool("solink") {
        if (debug) {
            optimize_level = "-O0"
        } else {
            optimize_level = "-O3"
        }
        default_output_extension = ".so"
        command = string_join(" ", ["$ndk_path/toolchains/llvm/prebuilt/darwin-x86_64/bin/clang -Wl,-soname,{{target_output_name}}$default_output_extension -shared -flto -target $android_target -o {{output}}", optimize_level, "{{inputs}}"])
        # target 选择 clang 具体工具链，指定 lto 参数 mcpu
        # 不能指定 -Os 否则会出错
        default_output_dir = "{{target_out_dir}}"
        outputs = linker_outputs
        output_prefix = "lib"
    }
}

toolchain("clang") {
    if (debug) {
        compiler_command = string_join(" ", ["-fembed-bitcode-marker", compiler_command])
    }
    if (code_coverage) {
        compiler_command = string_join(" ", ["-fprofile-instr-generate -fcoverage-mapping", compiler_command])
    }
    if (asan) {
        compiler_command = string_join(" ", ["-fsanitize=address", compiler_command])
    }
    if (ubsan) {
        compiler_command = string_join(" ", ["-fsanitize=undefined", compiler_command])
    }
    if (build_ios) {
        target_option = "-target $ios_archtecture-apple-ios9.0"
        if (ios_simulator) {
            target_option = string_join("-", [target_option, "simulator"])
            compiler_command = string_join(" ", ["-isysroot /Applications/Xcode.app/Contents/Developer/Platforms/iPhoneSimulator.platform/Developer/SDKs/iPhoneSimulator.sdk", compiler_command])
        } else {
            compiler_command = string_join(" ", ["-isysroot /Applications/Xcode.app/Contents/Developer/Platforms/iPhoneOS.platform/Developer/SDKs/iPhoneOS.sdk", compiler_command])
        }
        compiler_command = string_join(" ", [target_option, compiler_command])
    }

    tool("cc") {
        command = string_join(" ", ["clang", compiler_command, "{{cflags_c}}"])
        # depsformat = "gcc"
        #        command = string_join(" ", ["clang", compiler_command])
        outputs = compiler_outputs
    }

    tool("cxx") {
        command = string_join(" ", ["clang++", compiler_command, "{{cflags_cc}}"])
        # 目前只用于单元测试，所以不考虑异常模型
        # 默认 C++98 + libc++
        outputs = compiler_outputs
    }

    tool("stamp") {
    # source_set
        command = "touch {{output}}"
    }

    tool("link") {
        command = "clang++ -o {{output}} {{inputs}} {{frameworks}}"
        # 目前只用于单元测试，可以关闭 -fembed-bitcode 并使用 clang++
        # /tmp/lto.o 丢失会导致 dsymutil 无法生成 dSYM
        # -Xlinker -object_path_lto -Xlinker {{output_dir}}/{{target_output_name}}_lto.o
        if (code_coverage) {
            command = string_join(" ", [command, "-fprofile-instr-generate"])
        }
        if (asan) {
            command = string_join(" ", [command, "-fsanitize=address"])
        }
        if (ubsan) {
            command = string_join(" ", [command, "-fsanitize=undefined"])
        }
        default_output_dir = "{{root_out_dir}}"
        outputs = linker_outputs
    }

    tool("alink") {
        command = "libtool -static -o {{output}} {{inputs}}"
        default_output_dir = "{{target_out_dir}}"
        outputs = linker_outputs
        default_output_extension = ".a"
        output_prefix = "lib"
    }
}