static_library("llvh_demangle") {
    include_dirs = ["third_party/hermes/external/llvh/include"]
    sources = [
        "third_party/hermes/external/llvh/lib/Demangle/ItaniumDemangle.cpp",
        "third_party/hermes/external/llvh/lib/Demangle/MicrosoftDemangleNodes.cpp",
        "third_party/hermes/external/llvh/lib/Demangle/MicrosoftDemangle.cpp"
    ]
    cflags_cc = ["-std=c++11", "-fno-exceptions", "-fno-rtti", "-Wno-unused-parameter"]
}

static_library("llvh_support") {
    include_dirs = ["third_party/hermes/external/llvh/include", "third_party/hermes_include"]
    sources = [
        "third_party/hermes/external/llvh/lib/Support/APFloat.cpp",
        "third_party/hermes/external/llvh/lib/Support/APInt.cpp",
        "third_party/hermes/external/llvh/lib/Support/CommandLine.cpp",
        "third_party/hermes/external/llvh/lib/Support/ConvertUTF.cpp",
        "third_party/hermes/external/llvh/lib/Support/ConvertUTFWrapper.cpp",
        "third_party/hermes/external/llvh/lib/Support/Debug.cpp",
        "third_party/hermes/external/llvh/lib/Support/Errno.cpp",
        "third_party/hermes/external/llvh/lib/Support/Error.cpp",
        "third_party/hermes/external/llvh/lib/Support/ErrorHandling.cpp",
        "third_party/hermes/external/llvh/lib/Support/FileCheck.cpp",
        "third_party/hermes/external/llvh/lib/Support/FoldingSet.cpp",
        "third_party/hermes/external/llvh/lib/Support/GraphWriter.cpp",
        "third_party/hermes/external/llvh/lib/Support/Hashing.cpp",
        "third_party/hermes/external/llvh/lib/Support/Host.cpp",
        "third_party/hermes/external/llvh/lib/Support/InitLLVM.cpp",
        "third_party/hermes/external/llvh/lib/Support/LineIterator.cpp",
        "third_party/hermes/external/llvh/lib/Support/Locale.cpp",
        "third_party/hermes/external/llvh/lib/Support/MD5.cpp",
        "third_party/hermes/external/llvh/lib/Support/ManagedStatic.cpp",
        "third_party/hermes/external/llvh/lib/Support/Memory.cpp",
        "third_party/hermes/external/llvh/lib/Support/MemoryBuffer.cpp",
        "third_party/hermes/external/llvh/lib/Support/Mutex.cpp",
        "third_party/hermes/external/llvh/lib/Support/NativeFormatting.cpp",
        "third_party/hermes/external/llvh/lib/Support/Path.cpp",
        "third_party/hermes/external/llvh/lib/Support/PrettyStackTrace.cpp",
        "third_party/hermes/external/llvh/lib/Support/Process.cpp",
        "third_party/hermes/external/llvh/lib/Support/Program.cpp",
        "third_party/hermes/external/llvh/lib/Support/Regex.cpp",
        "third_party/hermes/external/llvh/lib/Support/SHA1.cpp",
        "third_party/hermes/external/llvh/lib/Support/Signals.cpp",
        "third_party/hermes/external/llvh/lib/Support/SmallPtrSet.cpp",
        "third_party/hermes/external/llvh/lib/Support/SmallVector.cpp",
        "third_party/hermes/external/llvh/lib/Support/SourceMgr.cpp",
        "third_party/hermes/external/llvh/lib/Support/Statistic.cpp",
        "third_party/hermes/external/llvh/lib/Support/StringExtras.cpp",
        "third_party/hermes/external/llvh/lib/Support/StringMap.cpp",
        "third_party/hermes/external/llvh/lib/Support/StringRef.cpp",
        "third_party/hermes/external/llvh/lib/Support/StringSaver.cpp",
        "third_party/hermes/external/llvh/lib/Support/TargetParser.cpp",
        "third_party/hermes/external/llvh/lib/Support/Threading.cpp",
        "third_party/hermes/external/llvh/lib/Support/Timer.cpp",
        "third_party/hermes/external/llvh/lib/Support/Triple.cpp",
        "third_party/hermes/external/llvh/lib/Support/Twine.cpp",
        "third_party/hermes/external/llvh/lib/Support/Unicode.cpp",
        "third_party/hermes/external/llvh/lib/Support/Valgrind.cpp",
        "third_party/hermes/external/llvh/lib/Support/Watchdog.cpp",
        "third_party/hermes/external/llvh/lib/Support/circular_raw_ostream.cpp",
        "third_party/hermes/external/llvh/lib/Support/raw_os_ostream.cpp",
        "third_party/hermes/external/llvh/lib/Support/raw_ostream.cpp",
        "third_party/hermes/external/llvh/lib/Support/regcomp.c",
        "third_party/hermes/external/llvh/lib/Support/regerror.c",
        "third_party/hermes/external/llvh/lib/Support/regexec.c",
        "third_party/hermes/external/llvh/lib/Support/regfree.c",
        "third_party/hermes/external/llvh/lib/Support/regstrlcpy.c",
    ]
    cflags = ["-Wno-unused-parameter"]
    cflags_cc = ["-std=c++11"]
    deps = [":llvh_demangle"]
}

config("napi_include") {
    include_dirs = [
        "include"
    ]
}

config("quickjs_include") {
    include_dirs = [
        "third_party/quickjs"
    ]
}

source_set("cutils") {
    public_configs = [":quickjs_include"]
    cflags_c = ["-Wno-sign-compare", "-Wno-unused-parameter"]
    sources = [
        "third_party/quickjs/cutils.c",
    ]
}

source_set("unicode") {
    public_configs = [":quickjs_include"]
    cflags_c = ["-Wno-sign-compare", "-Wno-unused-parameter"]
    sources = [
        "third_party/quickjs/libunicode.c"
    ]
}

source_set("regexp") {
    public_configs = [":quickjs_include"]
    cflags_c = ["-Wno-sign-compare", "-Wno-zero-length-array"]
    sources = [
        "third_party/quickjs/libregexp.c"
    ]
}

source_set("bf") {
    public_configs = [":quickjs_include"]
    cflags_c = ["-Wno-sign-compare", "-Wno-unused-parameter"]
    sources = [
        "third_party/quickjs/libbf.c"
    ]
}

config("quickjs_build") {
    cflags_c = ["-funsigned-char", "-Wno-unused-parameter", "-Wno-pedantic"]
}

source_set("quickjs_source_set") {
    public_configs = [":quickjs_include", ":quickjs_build"]
    defines = ["CONFIG_VERSION=\"2021-03-27\""]
    cflags_c = ["-Wno-sign-compare"]
    if (build_android || build_ios) {
        cflags_c += ["-Wno-unused-variable"]
    }
    if (ndk_path == "~/Library/Android/sdk/ndk/22.1.7171670") {
        cflags_c += ["-Wno-implicit-const-int-float-conversion"]
    }
    sources = [
        "third_party/quickjs/quickjs.c",
    ]
}
source_set("napi_common") {
    public_configs = [":napi_include"]
    sources = ["src/js_native_api_common.c"]
}
source_set("napi_qjs_source_set") {
    public_configs = [
        ":napi_include",
    ]
    configs = [
        ":quickjs_include",
        ":quickjs_build"
    ]
    sources = [
        "src/js_native_api_qjs.c",
    ]
}
if (build_android) {
    shared_library("quickjs") {
        public_configs = [":quickjs_include", ":quickjs_build"]
        deps = [":quickjs_source_set", ":cutils", ":unicode", ":regexp"]
        if (big_number) {
            deps += [":bf"]
        }
    }
    shared_library("napi_qjs") {
        deps = [":quickjs", ":napi_common", ":napi_qjs_source_set"]
    }
} else {
    source_set("napi_jsc_source_set") {
        public_configs = [
            ":napi_include",
        ]
        frameworks = [ "JavaScriptCore.framework" ]
        sources = [
            "src/js_native_api_jsc.c",
        ]
    }
    if (build_ios) {
        static_library("quickjs") {
            complete_static_lib = true
            deps = [":quickjs_source_set", ":cutils", ":unicode", ":regexp"]
            if (big_number) {
                deps += [":bf"]
            }
        }
        static_library("napi_qjs") {
            complete_static_lib = true
            configs = [":quickjs_include", ":quickjs_build"]
            deps = [":napi_qjs_source_set", ":napi_common"]
        }
        static_library("napi_jsc") {
            complete_static_lib = true
            deps = [":napi_jsc_source_set", ":napi_common"]
        }
    } else {
        source_set("test") {
            testonly = true
            include_dirs = [
                "test/include"
            ]
            cflags_cc = ["-std=c++11"]
            configs = [":napi_include"]
            sources = [
                "test/test.cpp",
                "test/general.cpp",
                "test/typeof.cpp",
                "test/conversion.cpp",
                "test/object.cpp",
                "test/callable.cpp",
                "test/reference.cpp"
            ]
            deps = [
                ":gtest",
            ]
        }

        executable("test_jsc") {
            testonly = true
            deps = [
                ":test",
                ":napi_jsc_source_set",
                ":napi_common"
            ]
        }

        executable("test_qjs") {
            testonly = true
            deps = [
                ":test",
                ":napi_qjs_source_set",
                ":napi_common",
                ":quickjs_source_set",
                ":cutils",
                ":unicode",
                ":regexp",
            ]
        }

        config("gtest_config") {
            include_dirs = [
                "third_party/googletest/googletest/include"
            ]
        }

        source_set("gtest") {
            testonly = true
            sources = [
                "third_party/googletest/googletest/src/gtest-death-test.cc",
                "third_party/googletest/googletest/src/gtest-filepath.cc",
                "third_party/googletest/googletest/src/gtest-matchers.cc",
                "third_party/googletest/googletest/src/gtest-port.cc",
                "third_party/googletest/googletest/src/gtest-printers.cc",
                "third_party/googletest/googletest/src/gtest-test-part.cc",
                "third_party/googletest/googletest/src/gtest-typed-test.cc",
                "third_party/googletest/googletest/src/gtest.cc",
            ]

            # Some files include "src/gtest-internal-inl.h".
            include_dirs = [ "third_party/googletest/googletest" ]

            public_configs = [ ":gtest_config" ]
        }
    }
}